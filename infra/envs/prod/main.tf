locals {
  name = "${var.project}-${var.environment}"
}

module "network" {
  source = "../../modules/network"

  name                 = local.name
  vpc_cidr             = var.vpc_cidr
  azs                  = var.azs
  public_subnet_cidrs  = var.public_subnet_cidrs
  private_subnet_cidrs = var.private_subnet_cidrs
  single_nat_gateway   = var.single_nat_gateway
  container_port       = var.container_port
  db_port              = 5432
}

module "rds" {
  source = "../../modules/rds"

  name                         = local.name
  private_subnet_ids           = module.network.private_subnet_ids
  rds_sg_id                    = module.network.rds_sg_id
  instance_class               = var.db_instance_class
  allocated_storage            = var.db_allocated_storage
  max_allocated_storage        = var.db_max_allocated_storage
  db_name                      = var.db_name
  db_username                  = var.db_username
  multi_az                     = var.db_multi_az
  backup_retention_days        = var.db_backup_retention_days
  deletion_protection          = var.db_deletion_protection
  skip_final_snapshot          = var.db_skip_final_snapshot
  performance_insights_enabled = var.db_performance_insights
  apply_immediately            = var.environment != "prod"
}

module "ecs" {
  source = "../../modules/ecs"

  name                    = local.name
  aws_region              = var.aws_region
  vpc_id                  = module.network.vpc_id
  public_subnet_ids       = module.network.public_subnet_ids
  private_subnet_ids      = module.network.private_subnet_ids
  alb_sg_id               = module.network.alb_sg_id
  ecs_sg_id               = module.network.ecs_sg_id
  container_image         = var.container_image
  container_port          = var.container_port
  task_cpu                = var.task_cpu
  task_memory             = var.task_memory
  desired_count           = var.desired_count
  log_retention_days      = var.log_retention_days
  container_insights      = var.container_insights
  alb_deletion_protection = var.alb_deletion_protection
  db_host                 = module.rds.address
  db_name                 = var.db_name
  db_secret_arn           = module.rds.master_user_secret_arn
}
